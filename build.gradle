plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'doore'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

/******* Start Spring Rest Docs *******/
ext {
    snippetsDir = file('build/generated-snippets')
}


tasks {
    test {
        useJUnitPlatform()
        outputs.dir snippetsDir
    }

    asciidoctor {
        doFirst {
            project.delete(files("src/main/resources/static/docs"))
        }
        inputs.dir snippetsDir
        configurations 'asciidoctorExt'
        dependsOn test
        baseDirFollowsSourceFile()
    }

    tasks.register('copyDocument', Copy) {
        dependsOn asciidoctor
        from file("${asciidoctor.outputDir}")
        into file("src/main/resources/static/docs")
    }

    bootJar {
        dependsOn asciidoctor
        from("${asciidoctor.outputDir}") {
            into "static/docs"
        }
    }

    build {
        dependsOn copyDocument
    }
}

/******* End Spring Rest Docs *******/

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation('com.amazonaws:aws-java-sdk-s3:1.12.470')
    implementation("org.springframework.boot:spring-boot-starter-data-redis")

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    // login
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    //restdocs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    //file
    compileOnly 'org.apache.tika:tika-parsers:1.20'

    // jsoup HTML parser library @ https://jsoup.org/
    implementation 'org.jsoup:jsoup:1.17.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'io.rest-assured:rest-assured:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}
